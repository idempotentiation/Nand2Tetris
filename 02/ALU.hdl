// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the output bits:
 * zr = (out == 0, 1, 0)
 * ng = (out < 0,  1, 0)
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // (out == 0, 1, 0)
        ng;      // (out < 0,  1, 0)

    PARTS:
    Mux16(a = x, b = false, sel = zx, out = w1);
    Mux16(a = y, b = false, sel = zy, out = w2);
    Not16(in = w1, out = w3);
    Not16(in = w2, out = w4);
    Mux16(a = w1, b = w3, sel = nx, out = w5);
    Mux16(a = w2, b = w4, sel = ny, out = w6);
    And16(a = w5, b = w6, out = w7);
    Add16(a = w5, b = w6, out = w8);
    Mux16(a = w7, b = w8, sel = f, out = w9);
    Not16(in = w9, out = w10);
    Mux16(a = w9, b = w10, sel = no, out = out, out[0..7] = zr1, out[8..15] = zr2, out[15] = ng);
    Or8Way(in = zr1, out = zr3);
    Or8Way(in = zr2, out = zr4);
    Or(a = zr3, b = zr4, out = zr5);
    Not(in = zr5, out = zr);
}
